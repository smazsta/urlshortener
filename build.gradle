plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.1'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.diffplug.spotless' version '6.25.0'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	// Spring Boot Starters
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	// Swagger
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'

	// URL validation
	implementation 'commons-validator:commons-validator:1.7'

	// Database (H2 for local testing)
	runtimeOnly 'com.h2database:h2'

	// Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// Testing
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.junit.jupiter:junit-jupiter-api'
	testImplementation 'org.junit.jupiter:junit-jupiter-engine'
	testImplementation 'org.mockito:mockito-junit-jupiter'
	testImplementation 'org.springframework.security:spring-security-test'

	// Lombok for tests
	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'
}

tasks.named('test') {
	useJUnitPlatform()
}

def javaFiles = project.fileTree(project.rootDir) {
	include '**/*.java'
	exclude 'build/**'
}

def xmlFiles = project.fileTree(project.rootDir) {
	include '**/*.xml'
	exclude 'build/**'
}

def yamlFiles = project.fileTree(project.rootDir) {
	include '**/*.yml'
	include '**/*.yaml'
	exclude 'build/**'
}

def markdownFiles = project.fileTree(project.rootDir) {
	include '**/*.md'
	exclude 'build/**'
}

spotless {
	java {
		target javaFiles
		indentWithSpaces(2)
		removeUnusedImports()
		trimTrailingWhitespace()
		importOrder 'static', ''
		endWithNewline()
//		licenseHeader('/*\n * Copyright (C) Smazsta, Inc.\n * All Rights Reserved.\n */', 'package')
	}

	format 'xml', {
		target xmlFiles
		eclipseWtp('xml')
		trimTrailingWhitespace()
		indentWithSpaces(2)
	}

	yaml {
		target yamlFiles
		prettier()
		trimTrailingWhitespace()
		indentWithSpaces(2)
	}

	format 'markdown', {
		target markdownFiles
		prettier()
		trimTrailingWhitespace()
		indentWithSpaces(2)
	}
}

// apply spotlessApply before commit
tasks.register('installPreCommitHook', Copy) {
	from file('config/git-hooks/pre-commit')
	into file('.git/hooks')
	fileMode 0755
}

build.dependsOn installPreCommitHook